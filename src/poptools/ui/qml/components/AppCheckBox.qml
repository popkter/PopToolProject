import QtQuick
import QtQuick.Controls
import "../theme"

CheckBox {
    id: control
    spacing: Theme.space8
    padding: Theme.space4
    font.pixelSize: Theme.fontBody
    implicitHeight: Math.max(Theme.controlHeight, contentItem.implicitHeight + topPadding + bottomPadding)
    indicator: Rectangle {
        x: control.leftPadding
        y: (control.height - height) / 2
        width: 22; height: 22; radius: Theme.radiusTiny
        opacity: control.enabled ? 1 : 0.5
        color: control.checked ? Theme.primary : Theme.inputDefault
        border.color: control.activeFocus ? Theme.primary : Theme.outline
        border.width: control.activeFocus ? 2 : 1
        MaterialIcon {
            anchors.centerIn: parent; icon: "check"; iconSize: 18
            visible: control.checked; color: Theme.primaryForeground
        }
    }
    contentItem: Text {
        text: control.text; font: control.font
        leftPadding: control.indicator.width + control.spacing
        color: control.enabled ? Theme.textPrimary : Theme.textSecondary
        verticalAlignment: Text.AlignVCenter; wrapMode: Text.Wrap
    }
}
